/**
 * Guava compatibility adapter.
 *
 * The tests are forked from Guava commit e370dde.
 */
dependencies {
  api project(':caffeine')
  api libraries.guava

  testImplementation testLibraries.junit
  testImplementation testLibraries.truth
  testImplementation testLibraries.jctools
  testImplementation testLibraries.guavaTestLib
}

tasks.named('compileJava').configure {
  modularity.inferModulePath = true
}

tasks.named('jar').configure {
  manifest {
    attributes 'Bundle-SymbolicName': 'com.github.ben-manes.caffeine.guava'
    attributes 'Import-Package': [
      'com.google.common.cache',
      'com.google.common.util.concurrent',
      'com.github.benmanes.caffeine.cache',
      'com.github.benmanes.caffeine.cache.stats'].join(',')
    attributes 'Export-Package': 'com.github.benmanes.caffeine.guava'
    attributes 'Automatic-Module-Name': 'com.github.benmanes.caffeine.guava'
  }
}

tasks.withType(Javadoc).configureEach {
  options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('test').configure {
  useJUnit {
    excludeCategories 'com.github.benmanes.caffeine.guava.OSGiTests'
  }
}

def osgiTests = tasks.register('osgiTests', Test) {
  group = 'Cache tests'
  description = 'Isolated OSGi tests'

  useJUnit {
    includeCategories 'com.github.benmanes.caffeine.guava.OSGiTests'
  }
}
tasks.named('test').configure {
  dependsOn(osgiTests)
}

tasks.withType(Test) {
  systemProperty 'guava.osgi.version', versions.guava
  systemProperty 'caffeine.osgi.jar', project(':caffeine').jar.archivePath.path
  systemProperty 'caffeine-guava.osgi.jar', project(':guava').jar.archivePath.path
}
